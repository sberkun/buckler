target C {
    timeout: 1 sec,
    tracing: true,
    clock-sync: off,
    coordination: decentralized
}

reactor Platform(start:int(0), expected_start:int(0), stp_offset_param:time(0)) {
    input in:int;
    output out:int;
    timer t(0, 100 msec);
    state count:int(start);
    state expected:int(expected_start);
    reaction(t) -> out {=
        SET(out, self->count++);
    =}
    reaction(in) {=
        info_print("Received %d.", in->value);
        if (in->value != self->expected_start++) {
            error_print_and_exit("Expected %d but got %d.", 
                self->expected_start - 1,
                in->value
            );
        }
    =} STP (stp_offset_param) {=
        info_print("Received %d late.", in->value);
        tag_t current_tag = get_current_tag();
        self->expected_start++;
        error_print("STP offset was violated by (%lld, %u).",
            current_tag.time - in->intended_tag.time,
            current_tag.microstep - in->intended_tag.microstep
        );
    =}
    reaction(shutdown) {=
        info_print("Shutdown invoked.");
        if (self->expected == self->expected_start) {
            error_print_and_exit("Did not receive anything.");
        }
    =}
}
federated reactor DecentralizedP2PComm {
    a = new Platform(expected_start = 100, stp_offset_param = 10 msec);
    b = new Platform(start = 100, stp_offset_param = 10 msec);
    a.out -> b.in;
    b.out -> a.in;
}
